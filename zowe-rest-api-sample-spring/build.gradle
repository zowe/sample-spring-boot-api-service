plugins {
    id 'org.springframework.boot' version '2.1.8.RELEASE'
    id 'java'
    id 'jacoco'
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'org.unbroken-dome.test-sets' version '2.2.1'
    id 'com.adarshr.test-logger' version '1.7.0'
}

apply plugin: 'io.spring.dependency-management'

group = 'org.zowe.sample'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

ext {
    sharedObjectFilePath = 'src/main/resources/lib/libzowe-sample.so'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    maven {
        url zoweArtifactoryRepository
        credentials {
            username zoweArtifactoryUser
            password zoweArtifactoryPassword
        }
    }
    jcenter()
    mavenCentral()
}

testSets {
    integrationTest {
        createArtifact = true
    }
}

integrationTest {
    outputs.upToDateWhen { false }
}

def commonsProject = { ->
        try {
            return project(':zowe-rest-api-commons-spring')
        } catch (ignored) {
            // See docs/versioning.md for more details
            return "org.zowe:zowe-rest-api-commons-spring:0.0.0-SNAPSHOT"
        }
    }

dependencies {
    implementation commonsProject()
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.rest-assured:rest-assured:4.1.1'
    testImplementation 'io.rest-assured:json-path:4.1.1'
    testImplementation 'io.rest-assured:xml-path:4.1.1'
    testImplementation 'io.rest-assured:spring-mock-mvc:4.1.1'
    testImplementation 'org.awaitility:awaitility:4.0.1'
    integrationTestAnnotationProcessor 'org.projectlombok:lombok'
}

jacoco {
    toolVersion = '0.8.4'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
    doLast {
        println "JaCoCo Test report written to: ${jacocoTestReport.reports.html.destination}/index.html"
    }
}

jacocoTestReport.dependsOn test

check.dependsOn jacocoTestReport

license {
    header rootProject.file('.licence/Apache-2.0-licence-header.txt')
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    excludes(["**/*.yml", "**/*.json", "**/*.sh", "**/*.txt", "**/*.p12", "**/*.xml", "**/*.jsp", "**/*.html", "**/*.jks", "**/*.so", "**/*.md"])
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

bootRun {
    args '--spring.config.additional-location=file:./config/local/application.yml'
}

task checkSharedObject {
    doFirst {
        def f = new File(projectDir, sharedObjectFilePath)
        if (!f.exists()) {
            throw new GradleException("Shared object $sharedObjectFilePath is missing. Run `./gradlew zosbuild` in directory $projectDir to build it yourself")
        }
    }
}

jar.dependsOn checkSharedObject

task zosbuild(type: Exec) {
    inputs.dir('zossrc').withPathSensitivity(PathSensitivity.RELATIVE)
    outputs.cacheIf { true }
    outputs.file(sharedObjectFilePath)
    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine 'zowe-api-dev.cmd', 'zosbuild'
    } else {
        commandLine 'npx', 'zowe-api-dev', 'zosbuild'
    }
}

testlogger {
    theme 'mocha'
}
