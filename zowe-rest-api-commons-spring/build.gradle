plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'jacoco'
    id 'com.github.hierynomus.license' version '0.15.0'
}

version getVersionFromGitTag()

ext {
    zoweApimlVersion = '1.1.2'
    springBootVersion = '2.1.7.RELEASE'
    sharedObjectFilePath = 'src/main/resources/lib/libzowe-commons-secur.so'
}

license {
    header rootProject.file('.licence/EPL-2.0-licence-header.txt')
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    excludes(["**/*.yml", "**/*.json", "**/*.sh", "**/*.txt", "**/*.p12", "**/*.xml", "**/*.jsp", "**/*.html", "**/*.jks", "**/*.so", "**/*.md"])
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
    maven {
        url zoweArtifactoryRepository
        credentials {
            username zoweArtifactoryUser
            password zoweArtifactoryPassword
        }
    }
    jcenter()
}

dependencies {
	implementation "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-security:$springBootVersion"
    implementation "com.ca.mfaas.sdk:mfaas-integration-enabler-java:$zoweApimlVersion"
	compileOnly 'org.projectlombok:lombok:1.18.8'
	annotationProcessor 'org.projectlombok:lombok:1.18.8'
	testImplementation "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
    testImplementation "net.javacrumbs.json-unit:json-unit-fluent:2.8.0"
}

task checkSharedObject {
    doFirst {
        def f = new File(projectDir, sharedObjectFilePath)
        if (!f.exists()) {
            throw new GradleException("Shared object $sharedObjectFilePath is missing. Run `./gradlew zosbuild` in directory $projectDir to build it yourself")
        }
    }
}

jar.dependsOn checkSharedObject

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                   'Implementation-Version': project.version)
    }
}

task saveVersion() {
    doLast {
        new File(projectDir, ".version").text = "$project.version"
    }
}

task zosbuild(type: Exec) {
    inputs.dir('zossrc').withPathSensitivity(PathSensitivity.RELATIVE);
    outputs.cacheIf { true };
    outputs.file(sharedObjectFilePath);
    commandLine 'zowe-api-dev', 'zosbuild';
}


apply from: "$projectDir/gradle/publishing.gradle"
